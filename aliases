# Productivity
alias w="cd ~/Workspaces"
alias c="cd ~/Workspaces/claude"
alias op="cd ~/Workspaces/opencode"
alias reload="exec zsh"

# Symlinks
alias symlinkls="find . -maxdepth 1 -type l -ls"
alias symlinkrm="find . -maxdepth 1 -type l -delete"

# Git
alias greadme="git add README.md && git commit -m 'chore: Update README.md' && git push"

# Initialize git, stage all, and generate commit message from diff
commit() {
  if [[ ! -d .git ]]; then
    printf '\033[33m=> Initializing git repository...\033[0m\n'
    git init
  fi

  printf '\033[34m=> Staging all files...\033[0m\n'
  git add -A

  local diff=$(git diff --cached --stat)
  if [[ -z "$diff" ]]; then
    printf '\033[33m=> No changes to commit.\033[0m\n'
    return 0
  fi

  printf '\033[36m%s\033[0m\n' "$diff"
  printf '\n\033[34m=> Generating commit message...\033[0m\n'
  local msg=$(git diff --cached | claude -p "
Write a commit message in the Conventional Commits format. Use the structure:
    <type>(<optional scope>): <short description>

    <optional body>

    <optional footer>

Example types: feat, fix, docs, style, refactor, perf, test, build, ci, chore, revert
Optionally, include a body for more details in bullet points.
Optionally, in the footer, use BREAKING CHANGE: followed by a detailed explanation of the breaking change.

Just return the commit message, do not include any other text.
")

  printf '\n\033[1m--- Proposed commit message ---\033[0m\n'
  printf '\033[32m%s\033[0m\n' "$msg"
  printf '\033[1m-------------------------------\033[0m\n\n'
  read "confirm?Commit with this message? [y/N] "
  if [[ "$confirm" =~ ^[Yy]$ ]]; then
    git commit -m "$msg"
    printf '\033[32m=> Committed successfully.\033[0m\n'
  else
    printf '\033[31m=> Commit aborted. Changes remain staged.\033[0m\n'
  fi
}

# Git Worktrees
alias gwl="git worktree list"
alias gwa="git worktree add"
alias gwr="git worktree remove"
alias gwp="git worktree prune"

gwab() {
  git worktree add -b "$1" "../$(basename $(pwd))-$1"
}

gwae() {
  git worktree add "../$(basename $(pwd))-$1" "$1"
}

gwao() {
  local worktree_path="../$(basename $(pwd))-$1"
  git worktree add --track -b "$1" "$worktree_path" "origin/$1"
  if [[ -f "$worktree_path/package.json" ]]; then
    echo "Installing dependencies..."
    (cd "$worktree_path" && npm install)
  fi
  cd "$worktree_path"
}

gwcd() {
  local wt=$(git worktree list | grep -i "$1" | head -1 | awk '{print $1}')
  [[ -n "$wt" ]] && cd "$wt" || echo "No worktree matching '$1'"
}

# Tmux
alias tmux="tmux -2"
alias tmux2="tmux -2"
alias tmux3="tmux -2"

# Claude Code Container
alias bccc="podman build -t claude-code-sandbox ~/Workspaces/leovanhaaren/llm-container"
alias ccc="podman run -it -v \$(pwd):/app -v ~/.claude:/home/claude/.claude -v ~/.claude.json:/home/claude/.claude.json claude-code-sandbox"

# Local overrides
[[ -f ~/.aliases.local ]] && source ~/.aliases.local

# Z.AI + Claude Code launcher
zai() {
  pass-cli inject --in-file "${DOTFILES_DIR}/claude/zai.zsh" | while read -r line; do
    eval "$line"
  done

  claude "$@"
}

mm() {
  pass-cli inject --in-file "${DOTFILES_DIR}/claude/minimax.zsh" | while read -r line; do
    eval "$line"
  done

  claude "$@"
}

